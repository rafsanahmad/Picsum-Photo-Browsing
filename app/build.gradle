plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.rafsan.picsumphotoapp"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            manifestPlaceholders = [authorities: "com.rafsan.picsumphotoapp.debug.fileprovider"]
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [authorities: "com.rafsan.picsumphotoapp.fileprovider"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'version'

    productFlavors {
        prod {
            dimension "version"
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            buildConfigField 'boolean', 'IS_PROD', 'true'
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField 'boolean', 'IS_PROD', 'false'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    //App Compat, layout, Core
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"

    //Material
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    //Room
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"

    // Activity KTX
    implementation "androidx.activity:activity-ktx:$rootProject.ktxActivity"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpLoggingVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutineVersion"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$rootProject.hiltVersion"

    //Swipe refresh layout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.swpRefreshLayout"

    //Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    implementation "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    //Paging
    implementation "androidx.paging:paging-runtime-ktx:$rootProject.pagingLibVersion"

    //idling resource
    implementation "androidx.test.espresso:espresso-idling-resource:$rootProject.espressoCore"

    //Photo view
    implementation "com.github.chrisbanes:PhotoView:$rootProject.photoView"

    //Testing dependencies
    testImplementation 'junit:junit:4.+'
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoCoreVersion"
    testImplementation "org.mockito:mockito-inline:$rootProject.mockitoInlineVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$rootProject.mockitoKotlin"
    testImplementation "androidx.arch.core:core-testing:$rootProject.archCoreTest"
    testImplementation "androidx.test:core:$rootProject.coreTest"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectricVersion"
    testImplementation "com.google.truth:truth:$rootProject.truthVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$rootProject.mockWebserver"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.coroutineTestVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$rootProject.kotlinVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$rootProject.archCoreTest"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.jUnitVersion"
    androidTestImplementation "com.google.truth:truth:$rootProject.truthVersion"
    androidTestImplementation("androidx.test.espresso:espresso-core:$rootProject.espressoCore") {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$rootProject.espressoCore") {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation("androidx.test.espresso:espresso-intents:$rootProject.espressoCore") {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation "androidx.test.ext:junit-ktx:$rootProject.junitKtxVersion"
    androidTestImplementation "androidx.test:core-ktx:$rootProject.coreTest"
}